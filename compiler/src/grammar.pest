Program = { SOI ~ (Stmt)* ~ EOI }

Stmt = {
    ExprStmt |
    PrintStmt |
    ReturnStmt |
    VarDefnStmt |
    IfElseStmt |
    IfStmt |
    WhileLoop |
    Block
}

ExprStmt = { Expr ~ SEMI }
PrintStmt = { "print" ~ Expr ~ SEMI }
VarDefnStmt = { "let" ~ ID ~ "=" ~ Expr ~ SEMI }
ReturnStmt = { "return" ~ Expr ~ SEMI }
IfStmt = { "if" ~ "(" ~ Expr ~ ")" ~ Block }
IfElseStmt = { "if" ~ "(" ~ Expr ~ ")" ~ Block ~ "else" ~ Block }
Block = { "{" ~ (Stmt)* ~ "}" }
WhileLoop = {
    "while" ~ "(" ~ Expr ~ ")" ~ Block
}


Expr = { Comparand ~ (CompExpr)* }
CompExpr = { (GEQ | GT | EQ | NEQ | LEQ | LT) ~ Comparand }
Comparand = { Factor ~ (AddExpr)* }
AddExpr = { (PLUS | MINUS) ~ Factor }
Factor = { Primary ~ (MulExpr)* }
MulExpr = { (TIMES | DIVIDE) ~ Primary }

Primary = {
    ParenExpr |
    Number |
    TRUE |
    FALSE |
    VariableAccess |
    UnaryExpr
}

TRUE = { "true" }
FALSE = { "false" }

VariableAccess = { ID }

ParenExpr = {
    "(" ~ Expr ~ ")"
}

UnaryExpr = { (NEG | NOT) ~ Primary }

PLUS = { "+" }
MINUS = { "-" }

TIMES = { "*" }
DIVIDE = { "/" }

NEG = { "-" }
NOT = { "!" }
SEMI = { ";" }

GEQ = { ">=" }
GT = { ">" }
LEQ = { "<=" }
LT = { "<" }
EQ = { "==" }
NEQ = { "!=" }

Number = @{ ("-")? ~ (ASCII_DIGIT)+ }

ID = @{ 
    !(Reserved) ~
    ( ASCII_LETTER ~ (ASCII_LETTER | ASCII_DIGIT | "_")* )
}

Reserved = {
    "print" | "let" | "if" | "else" | "return"
}

ASCII_LETTER = _{ ('A'..'Z') | ('a'..'z') }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
