Program = { SOI ~ (Stmt)* ~ EOI }

Stmt = {
    ExprStmt |
    PrintStmt |
    ReturnStmt |
    VarDefnStmt |
    Block
}

ExprStmt = { Expr ~ SEMI }
PrintStmt = { "print" ~ Expr ~ SEMI }
VarDefnStmt = { "let" ~ ID ~ "=" ~ Expr ~ SEMI }
ReturnStmt = { "return" ~ Expr ~ SEMI }
Block = { "{" ~ (Stmt)* ~ "}" }


Expr = { Factor ~ (AddExpr)* }
AddExpr = { (PLUS | MINUS) ~ Factor }
Factor = { Primary ~ (MulExpr)* }
MulExpr = { (TIMES | DIVIDE) ~ Primary }

Primary = {
    ParenExpr |
    Number |
    VariableAccess |
    UnaryExpr
}

VariableAccess = { ID }

ParenExpr = {
    "(" ~ Expr ~ ")"
}

UnaryExpr = { NEG ~ Primary }

PLUS = { "+" }
MINUS = { "-" }

TIMES = { "*" }
DIVIDE = { "/" }

NEG = { "-" }
SEMI = { ";" }

Number = @{ ("-")? ~ (ASCII_DIGIT)+ }

ID = @{ ASCII_LETTER ~ (ASCII_LETTER | ASCII_DIGIT | "_")* }

ASCII_LETTER = _{ ('A'..'Z') | ('a'..'z') }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
